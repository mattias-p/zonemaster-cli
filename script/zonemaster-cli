#!/usr/bin/env perl

use 5.14.2;
use warnings;

use Zonemaster::CLI;
use File::Spec;
use autodie;

sub read_conf_file {
    # Returns list of command line parameters. List can be empty.
    my ($conf_file) = @_;
    my @lines;
    open my $fh, '<', $conf_file;
    while (<$fh>) {
        chomp;
        next if /^\s*$/;
        next if /^\s*#/;
        push @lines, $_;
    };
    return @lines;
}

# Load default arguments from file in home directory, if any
# This must be loaded before any global file to make the local
# file take precedence
my $home_dir  = ((getpwuid($<))[7]) || $ENV{HOME};
my $home_conf_file = File::Spec->catfile($home_dir, '.zonemaster', 'cli.args');

if (-r $home_conf_file) {
    my @lines = read_conf_file ($home_conf_file);
    unshift @ARGV, @lines;
}

# Load default arguments from global file, if any
my @global_conf = (
    '/etc/zonemaster/cli.args',
    '/usr/local/etc/zonemaster/cli.args'
    ); # Order is significant.
my $global_conf_file;

for my $p (@global_conf) {
    if ( -e $p and -r $p ) {
        $global_conf_file = $p;
        last;
    }
}

if ( defined $global_conf_file ) {
    my @lines = read_conf_file ($global_conf_file);
    unshift @ARGV, @lines;

}

eval {
    my $exitstatus = Zonemaster::CLI->run( @ARGV );
    exit $exitstatus;
};

print STDERR $@;
exit $Zonemaster::CLI::EXIT_GENERIC_ERROR;

=encoding UTF-8

=head1 NAME

zonemaster-cli - run Zonemaster tests from the command line

=head1 USAGE

    zonemaster-cli --help
    zonemaster-cli --version
    zonemaster-cli --dump-profile
    zonemaster-cli --list-tests
    zonemaster-cli [OPTIONS] DOMAIN_NAME

=head1 DESCRIPTION

L<zonemaster-cli> is a command-line interface to the L<Zonemaster> test engine.
It takes instructions the user provides as command line arguments, transforms
them into suitable API calls to the engine, runs the test suite and prints the
resulting messages. By default, the messages will be translated by the engine's
translation module, with the corresponding timestamp and logging level when
printed. See the available options below.

=head1 OPTIONS

=over

=item --help -h --usage -?

Print this usage information.

=item --version

Print the versions of this program as well as the ones from the underlying
L<Zonemaster> test engine.

=item --list-tests, --list_tests

List all available tests.

=item --dump-profile, --dump_profile

Print the effective profile used in JSON format.

=item --test=MODULE, --test=MODULE/TESTCASE, --test=TESTCASE

Limit the run to only include the specified test.
Repeat this option to include multiple tests.
Should be the case-insensitive name of a test module (e.g. "Delegation") and/or
a test case (e.g. "Delegation/delegation01" or "delegation01").
This option may be repeated.
Default: all tests in the testings suite

=item --profile=FILE

Override the Zonemaster Engine default profile data with values from
the given profile JSON file.

=item --[no-]ipv4

Allow the sending of IPv4 packets.
Default: on

=item --[no-]ipv6

Allow the sending of IPv6 packets.
Default: on

=item --sourceaddr4=IPADDR

Source IPv4 address used when sending queries.
Setting an IPv4 address not correctly configured on a local network interface
fails silently.

=item --sourceaddr6=IPADDR

Source IPv6 address used when sending queries.
Setting an IPv6 address not correctly configured on a local network interface
fails silently.

=item --ns=DOMAINNAME, --ns=DOMAINNAME/IP

Override the parent-side delegation information.
This option may be given multiple times.
If no IP is provided, the name is looked up for IP addresses.

=item --ds=KEYTAG,ALGORITHM,TYPE,DIGEST

Provide a DS record for undelegated testing (that is, a test where the
delegating nameserver information is given via --ns switches). The four pieces
of data (keytag, algorithm, type, digest) should be in the same format they would
have in a zone file.

=item --hints=FILENAME

Name of a root hints file to override the defaults.

=item --save=FILENAME

Save DNS data to file after running tests.

=item --restore=FILENAME

Restore DNS data from file before running test.

=item --[no-]progress

Print an activity indicator ("spinner"). Useful to know that something is
happening during a run.
Default: on if STDOUT is a tty, off if it is not.

=item --level=LEVEL

The minimum severity level required for messages to be displayed.
Must be one of CRITICAL, ERROR, WARNING, NOTICE, INFO, DEBUG, DEBUG2 or DEBUG3.
Default: NOTICE

=item --stop-level=LEVEL, --stop_level=LEVEL

As soon as a message at this level or higher is logged, the run is aborted.
Must be one of CRITICAL, ERROR, WARNING, NOTICE, INFO, DEBUG, DEBUG2 or DEBUG3.

=item --[no-]time

Show the timestamp of each message.
Default: on

=item --[no-]show-level, --[no-]show_level

Show the severity level of each message.
Default: on

=item --[no-]show-module, --[no-]show_module

Show which test module produced each message.
Default: off

=item --[no-]show-testcase, --[no-]show_testcase

Show which test case produced each message.
Default: off

=item --[no-]json

Print results as a JSON object instead of a table.
Default: off

=item --[no-]json-stream, --[no-]json_stream

Print results as a newline-delimited JSON stream instead of a table.
Default: off

=item --[no-]raw

Show raw message identifiers instead of human language messages.

=item --locale=LOCALE

The locale to be used for message translation. If not given, the locale is
determined from standard environment variables. If the requested translation
does not exist, it will fallback to the local locale, and if that doesn't exist
either, to English.

=item --[no-]json-translate, --[no-]json_translate

Deprecated since v2023.1, use --no-raw instead.

For streaming JSON output, include the translated message of the tag.

=item --[no-]count

At the end of a run, print a summary of the number of messages at each level.
Default: off

=item --nstimes

At the end of a run, print a summary of the times (in milliseconds) the zone's
name servers took to answer.

=item --[no-]elapsed

Print elapsed time (in seconds) at end of a run.
Default: off

=item --encoding=ENCODING

Deprecated: Simply remove it from your usage. It is ignored.

=back

=head1 PROFILES

The testing and result analysis performed by Zonemaster Engine is always
guided by a profile.
Zonemaster Engine has a default profile with sensible defaults.
Zonemaster CLI allows users to override the default profile data with
values from a profile JSON file with the C<--profile> option.
For details on profiles and how they're respresented in files, see
L<Zonemaster::Engine::Profile>.

=head1 CONFIGURATION

If there is a readable file F</etc/zonemaster/cli.args> (Linux style), each line
in that file will be prepended as an argument on the command line. If no
F</etc/zonemaster/cli.args> is found (or is not readable) but
F</usr/local/etc/zonemaster/cli.args> (FreeBSD style) is found and readable then
that file will be used instead. Only one global file is loaded.

If there is a readable file F<.zonemaster/cli.args> in the user's home
directory, it will be used in the same way even when a global file has been
loaded. Any argument in user's F<cli.args> will override the same argument in the
global config file.

For example, if one would like to by default run with the log
level set to DEBUG and with translation to human-readable messages turned off,
one could put this in the config file:

   --raw
   --level=DEBUG

Only one argument per line. If the argument has a value there must be a "="
between argument and value. A line starting with "#" is a comment. Comments
cannot be added on lines with arguments.

Any arguments actually given on the command line will override what is in any of
the loaded config files.
